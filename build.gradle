/*******************************************************************************
 * Copyright 2014 Carlos Macasaet
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

import org.gradle.plugins.signing.Sign

group = 'com.macasaet.lambda'
ext.archivesBaseName = 'fluent-lambda'
version = '0.1.1-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith( 'SNAPSHOT' )
project.ext.sourceCompatibility = 1.5

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
  }
}

gradle.taskGraph.whenReady {
  taskGraph ->
    if( taskGraph.hasTask( ':uploadArchives' )
        && taskGraph.allTasks.any { it instanceof Sign } )
    {
      Console console = System.console()
      def password =
        console.readPassword( "\n\nPGP Password for Private Key "
                              + ext."signing.keyId" + ": " )
      allprojects { ext."signing.password" = password }
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'nexus'

repositories {
  mavenCentral()
}

jar {
  manifest {
    attributes 'Specification-Title': 'Fluent Lambda',
      'Specification-Version': '0.0',
      'Implementation-Title': group,
      'Implementation-Version': version
  }
}

task writeNewPom << {
  pom {
    project {
      inceptionYear '2014'
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }.writeTo( "${buildDir/newpom.xml}" )
}

modifyPom {
  project {
    name 'Fluent Lambda'
    packaging 'jar'
    artifactId archivesBaseName
    description 'Fluent interfaces for creating lambda expressions in Java'
    url 'https://github.com/l0s/fluent-lambda'

    scm {
      connection 'scm:git@github.com:l0s/fluent-lambda.git'
      developerConnection 'scm:git@github.com:l0s/fluent-lambda.git'
      url 'https://github.com/l0s/fluent-lambda'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
      }
    }

    developers {
      developer {
        id 'l0s'
        name 'Carlos Macasaet'
      }
    }
  }
}

nexus {
  attachSources = true
  attachTests = false
  attachJavadoc = true
  sign = true
  repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
  snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

dependencies {
  compile group: 'com.google.guava', name: 'guava', version: '10.0'
  compile group: 'cglib', name: 'cglib', version: '2.2.2'
  compile group: 'commons-lang', name: 'commons-lang', version: '2.5'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'

  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.apache.commons', name: 'commons-math', version: '2.2'
  testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.1'
}
